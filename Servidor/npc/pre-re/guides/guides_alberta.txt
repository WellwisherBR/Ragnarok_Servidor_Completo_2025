// ===== Rathena script ========================================
// = Guias de Alberta 
// ===== por: =====================================================
// = kobra_k88;L0NE_W0LF
// ===== Versão atual: ======================================
// = 1.5a
// ===== Compatível com: =======================================
// = projeto Rathena
// ===== Descrição: ==========================================
// = [conversão oficial]
// = guias para a cidade de Alberta
// ===== Comentários adicionais: ===================================
// = funcionando totalmente
// = 1,1 coordenados de arsenal fixos, graças à equipe de Freya
// = 1.2 Nomes de guia comuns otimizados e atualizados [lúpus]
// = 1.3 Atualizou o guia de acordo com como é na IRO, obrigado [masterofmuppets]
// = para muad_dib
// = 1.4 ajustou as muitas linhas em 1 ou 2 e corrigiu alguns erros de digitação [erkurita]
// = 1,5 Rescrito aos padrões oficiais de 10.3.[L0ne_w0lf]
// = 1.5 Corrigido alguns erros com o loop [Samuray22]
// -Obrigado a $ Ephiroth.
//============================================================

alberta,23,238,4	script	Guide#alb::AlbGuide	105,{
	cutin "prt_soldier",2;
	mes "[Guia de Alberta]";
	mes "Bem -vindo a Alberta, a cidade portuária.";
	mes "Sinta-se à vontade para me perguntar se você está tendo problemas para encontrar alguma coisa na cidade ou se você só precisa de orientação pela cidade.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guia da cidade:Remova as marcas do Mini-Mapa:Perceber:Cancelar")) {
		case 1:
			mes "[Alberta Guide]";
			mes "Selecione um local no menu a seguir.";
			if (.@compass_check == 0) {
				mes "Você gostaria que eu marque os locais em seu Mini-Mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Mercante Guild^000000:Loja de armas:Loja de ferramentas:Pousada:Forja:Cancelar")) {
				case 1:
					mes "[Guia de Alberta]";
					mes "O Merchant Guild lida com mudanças de classe na classe de comerciantes e está localizado no canto sudoeste de Alberta.";
					if (.@compass_check)
						viewpoint 1,33,41,2,0xFF0000;
					break;
				case 2:
					mes "[Guia de Alberta]";
					mes "A loja de armas pode ser encontrada no extremo sul de Alberta.";
					if (.@compass_check)
						viewpoint 1,117,37,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia de Alberta]";
					mes "A loja de ferramentas está meio perto do centro de Alberta.";
					mes "Não deve ser muito difícil de encontrar.";
					if (.@compass_check)
						viewpoint 1,98,154,4,0xFF00FF;
					break;
				case 4:
					mes "[Guia de Alberta]";
					mes "Há uma pousada no extremo norte de Alberta, onde você pode descansar.";
					if (.@compass_check)
						viewpoint 1,65,233,5,0xFF00FF;
					break;
				case 5:
					mes "[Guia de Alberta]";
					mes "A forja em Alberta está no mesmo edifício que a Guilda do Mercante.";
					mes "É para o sudoeste.";
					if (.@compass_check)
						viewpoint 1,35,41,6,0xFF00FF;
					break;
				case 6:
					mes "[Guia de Alberta]";
					mes "Por favor, peça-me para 'Remover Marcas do Mini-Mapa' se você não deseja mais ter as marcas de localização exibidas no seu Mini-Mapa.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0xFF00FF;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,175,220,6,0xFF00FF;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Alberta]";
			mes "Avanços na feitiçaria e tecnologia nos permitiram atualizar nosso sistema de informações,";
			mes "permitindo marcar locais em seu Mini-Mapa para facilitar a navegação.";
			next;
			mes "[Guia de Alberta]";
			mes "Seu Mini-Mapa está localizado no canto superior direito da tela.";
			mes "Se não o conseguir ver, aperte as teclas Ctrl + Tab ou clique no botão 'Mapa' na Janela de Informação Básica.";
			next;
			mes "[Guia de Alberta]";
			mes "No seu Mini-Mapa, clique nos símbolos '+' e '-' para aumentar o zoom e o seu Mini-Mapa.";
			mes "Esperamos que você aproveite suas viagens aqui na cidade de Alberta.";
			break;
		case 4:
			mes "[Guia de Alberta]";
			mes "Viajem em segurança e não hesitem em perguntar-me se têm alguma dúvida sobre Alberta.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "prt_soldier",255;
	end;
}

alberta,120,60,3	duplicate(AlbGuide)	Guide#2alb	105
