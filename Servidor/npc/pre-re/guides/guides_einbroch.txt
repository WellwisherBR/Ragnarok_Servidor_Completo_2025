// ===== Rathena script ========================================
// = guias einbroch
// ===== por: =====================================================
// = muad_dib;L0NE_W0LF
// ===== Versão atual: ======================================
// = 1.5
// ===== Compatível com: =======================================
// = projeto Rathena
// ===== Descrição: ==========================================
// = [conversão oficial]
// = guias para a cidade de Einbroch
// ===== Comentários adicionais: ===================================
// = 1,0b abas ausentes, coordenar guia corrigido [lúpus]
// = 1.1 Adicionado 1 mais guia NPC, Thanx a Justin84 [Lupus]
// = 1.2 Nomes de guia comuns otimizados e atualizados [lúpus]
// = 1.3 ajustou as muitas linhas em 1 ou 2 e corrigiu alguns erros de digitação [erkurita]
// = 1,4 Duplicatas removidas [silencioso]
// = 1,5 Rescrito aos padrões oficiais de 10.3.[L0ne_w0lf]
//============================================================

einbroch,72,202,4	script	Guide#ein::EinGuide	852,{
	cutin "ein_soldier",2;
	mes "[Guia Einbroch]";
	mes "Bem-vindo a Einbroch, a cidade de aço.";
	mes "Por favor me pergunte se você tem alguma dúvida.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guia da cidade.:Remova as marcas do mini-mapa.:Notícias.:Cancelar.")) {
		case 1:
			mes "[Guia Einbroch]";
			mes "Selecione um local no menu seguinte.";
			if (.@compass_check == 0) {
				mes "Você gostaria que eu marque os locais em seu mini-mapa?";
				next;
				if (select("Sim.:Não.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("^FF0000Aeroporto^000000:Estação ferroviária:Fábrica:Praça:Hotel:Loja de armas:Laboratório:Guilda dos Ferreiros:Torre Einbroch:Cancelar")) {
				case 1:
					mes "[Guia Einbroch]";
					mes "O ^FF0000Aeroporto^000000 está localizado na parte noroeste da cidade.";
					mes "Lá você pode ver o orgulho e a alegria de nossa cidade, o dirigível.";
					mes "Lembre-se de que você deve pagar a admissão para embarcar no dirigível.";
					if (.@compass_check)
						viewpoint 1,63,228,2,0xFF0000;
					break;
				case 2:
					mes "[Guia Einbroch]";
					mes "A estação de trem está localizada na parte nordeste de Einbroch.";
					mes "Os trens correndo entre aqui e Einbech correm o dia inteiro, todos os dias.";
					if (.@compass_check)
						viewpoint 1,236,279,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia Einbroch]";
					mes "A fábrica, talvez a instalação mais importante em Einbroch, está localizada na parte sul da cidade.";
					if (.@compass_check)
						viewpoint 1,158,78,4,0xFF00FF;
					break;
				case 4:
					mes "[Guia Einbroch]";
					mes "O Plaza, nosso maior distrito comercial, pode ser encontrado a leste do centro de Einbroch.";
					if (.@compass_check)
						viewpoint 1,232,190,5,0xFF00FF;
					break;
				case 5:
					mes "[Guia Einbroch]";
					mes "O hotel fica a leste da praça e oferece acomodações de alto calibre.";
					mes "Lá, você pode aproveitar a sua estadia em Einbroch em conforto~";
					if (.@compass_check)
						viewpoint 1,260,201,6,0x00FF00;
					break;
				case 6:
					mes "[Guia Einbroch]";
					mes "A loja de armas está localizada ao norte da praça.";
					mes "Lá você pode comprar armas para seu uso pessoal.";
					if (.@compass_check)
						viewpoint 1,215,221,7,0x00FF00;
					break;
				case 7:
					mes "[Guia Einbroch]";
					mes "O laboratório é um anexo da fábrica e está localizado no setor sudoeste de Einbroch.";
					if (.@compass_check)
						viewpoint 1,36,49,8,0x00FF;
					break;
				case 8:
					mes "[Guia Einbroch]";
					mes "A Blacksmith Guild está localizada na parte sudeste de Einbroch.";
					mes "Você pode atualizar seu equipamento usando seus serviços.";
					if (.@compass_check)
						viewpoint 1,244,90,9,0x00FF00;
					break;
				case 9:
					mes "[Guia Einbroch]";
					mes "A Torre Einbroch está localizada no centro da cidade.";
					mes "Do topo da torre, você pode ver todo o Einbroch.";
					if (.@compass_check)
						viewpoint 1,174,195,10,0xFFFF00;
					break;
				case 10:
					mes "[Guia Einbroch]";
					mes "Por favor, peça-me para 'remover as marcas do mini-mapa' se você não deseja mais ter as marcas de localização exibidas no seu mini-mapa.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,63,228,2,0xFF0000;
			viewpoint 2,236,279,3,0xFF00FF;
			viewpoint 2,158,78,4,0xFF00FF;
			viewpoint 2,232,190,5,0xFF00FF;
			viewpoint 2,260,201,6,0x00FF00;
			viewpoint 2,215,221,7,0x00FF00;
			viewpoint 2,36,49,8,0x00FF00;
			viewpoint 2,244,90,9,0x00FF00;
			viewpoint 2,174,195,10,0xFFFF00;
			set .@compass_check,0;
			mes "[Guia Einbroch]";
			mes "Ok, as marcas do seu mini-mapa foram removidas.";
			mes "Se você precisar de alguma orientação em torno de Einbroch, por favor, deixe-me ou um dos outros guias de einbroch saber.";
			break;
		case 3:
			mes "[Guia Einbroch]";
			mes "Através da tecnologia da República de Schwarzwald, atualizamos para um sistema de informações digitais que nos permite marcar locais em seu mini-mapa para facilitar a navegação.";
			next;
			mes "[Guia Einbroch]";
			mes "Seu mini-mapa está localizado no canto superior direito da tela.";
			mes "Se você não puder vê-lo, pressione as teclas CTRL + TAB ou clique no botão 'Mapa' na sua janela de informações básicas.";
			next;
			mes "[Guia Einbroch]";
			mes "No seu mini-mapa, clique nos símbolos '+' e '-' para aumentar o zoom e o seu mini-mapa.";
			mes "Esperamos que você aproveite suas viagens aqui em Einbroch, aventureiro.";
			break;
		case 4:
			mes "[Guia Einbroch]";
			mes "Esperamos que você aproveite suas viagens aqui em Einbroch.";
			mes "Ah, e por favor, esteja ciente dos alertas de poluição.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbroch,155,43,4	duplicate(EinGuide)	Guide#2ein	852
einbroch,162,317,4	duplicate(EinGuide)	Guide#3ein	852

einbech,67,37,4	script	Guide#4ein::EinGuide2	852,{
	cutin "ein_soldier",2;
	mes "[Guia Einbech ]";
	mes "Bem-vindo a Einbech, a cidade de mineração.";
	mes "Estamos aqui para ajudar os turistas. portanto, se você tiver alguma dúvida, sinta-se à vontade para nos perguntar.";
	mes "";
	mes "";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guia da cidade.:Remova as marcas do mini-mapa.:Notícias.:Cancelar.")) {
		case 1:
			mes "[Guia Einbech ]";
			mes "Selecione um local no menu a seguir.";
			if (.@compass_check == 0) {
				mes "Você gostaria que eu marque os locais em seu mini-mapa?";
				next;
				if (select("Sim.:Não.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("Estação ferroviária:Taberna:Loja de ferramentas:Guilda dos Swordman:Mina:Cancelar")) {
				case 1:
					mes "[Guia Einbech ]";
					mes "As estações de trem estão localizadas nas partes noroeste e nordeste de Einbech.";
					mes "Lá, você pode pegar um trem para Einbroch.";
					if (.@compass_check)
						viewpoint 1,43,213,2,0xFF0000;
					break;
				case 2:
					mes "[Guia Einbech ]";
					mes "A taberna está localizada na parte sul de Einbech.";
					mes "É um bom lugar para relaxar depois de um longo dia.";
					if (.@compass_check)
						viewpoint 1,142,112,3,0xFF00FF;
					break;
				case 3:
					mes "[Guia Einbech ]";
					mes "Você pode encontrar a loja de ferramentas no centro de Einbech.";
					mes "Lá, você pode comprar qualquer ferramenta necessária para suas viagens.";
					if (.@compass_check)
						viewpoint 1,176,136,4,0xFF00FF;
					break;
				case 4:
					mes "[Guia Einbech ]";
					mes "A Guilda dos Swordman está localizado nos arredores orientais de Einbech.";
					mes "Está em construção e eles não começaram a aceitar aplicações.";
					if (.@compass_check)
						viewpoint 1,250,110,5,0xFF00FF;
					break;
				case 5:
					mes "[Guia Einbech ]";
					mes "A mina, que é a principal indústria de Einbech, está localizada na parte norte desta cidade.";
					mes "É onde temos todos os nossos minérios, embora os monstros entrem no caminho dos mineiros.";
					if (.@compass_check)
						viewpoint 1,138,251,6,0x00FF00;
					break;
				case 6:
					mes "[Guia Einbech ]";
					mes "Por favor, peça-me para 'remover as marcas do mini-mapa' se você não deseja mais ter as marcas de localização exibidas em seu mini-mapa.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,43,213,2,0xFF0000;
			viewpoint 2,142,112,3,0xFF00FF;
			viewpoint 2,176,136,4,0xFF00FF;
			viewpoint 2,250,110,5,0xFF00FF;
			viewpoint 2,138,251,6,0x00FF00;
			set .@compass_check,0;
			mes "[Guia Einbech ]";
			mes "Ok, as marcas do seu mini-mapa foram removidas.";
			mes "Se você precisar de alguma orientação sobre a Einbech, informe-me ou um dos outros guias da Einbech.";
			break;
		case 3:
			mes "[Guia Einbech ]";
			mes "Através da tecnologia da República de Schwarzwald,";
			mes "Atualizamos para um sistema de informações digitais que nos permite marcar locais no seu mini-mapa para facilitar a navegação.";
			next;
			mes "[Guia Einbech ]";
			mes "Seu mini-mapa está localizado no canto superior direito da tela.";
			mes "Se você não pode vê-lo, pressione Ctrl + Tab Ou clique no botão 'Mapa' na sua janela de informações básicas.";
			next;
			mes "[Guia Einbech ]";
			mes "No seu mini-mapa, clique nos símbolos '+' e '-' para aumentar o zoom e o seu mini-mapa.";
			mes "Esperamos que você aproveite suas viagens aqui em Einbech, aventureiro.";
			break;
		case 4:
			mes "[Guia Einbech ]";
			mes "Esperamos que você aproveite suas viagens aqui em Einbech.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbech,48,214,4	duplicate(EinGuide2)	Guide#5ein	852
