// ===== Rathena script ========================================
// = guias de geffen
// ===== por: =====================================================
// = kobra_k88;L0NE_W0LF
// ===== Versão atual: ======================================
// = 1.4a
// ===== Compatível com: =======================================
// = projeto Rathena
// ===== Descrição: ==========================================
// = [conversão oficial]
// = guias para a cidade de Geffen
// ===== Comentários adicionais: ===================================
// = funcionando totalmente.Adicionado um guia a cada saída.
// = 1.1 agora usando o comando duplicado.
// = 1.2 Nomes de guia comuns otimizados e atualizados [lúpus]
// = 1,2a Nota pequena que o BS Guild mudou-se para Einbroch [Poki#3]
// = 1,3 duplicatas removidas [silencioso]
// = 1.4A Rescrito aos padrões oficiais de 10.3.[L0ne_w0lf]
// = 1.4 Corrigido alguns erros com o loop [samuray22]
// = Obrigado a $ Ephiroth.
//============================================================

geffen,203,116,0	script	Guide#gef::GefGuide	705,{
	cutin "gef_soldier",2;
	mes "[Guia de Geffen]";
	mes "Bem-vindo a Geffen, a cidade da magia.";
	mes "Se você precisar de alguma orientação pela cidade, fique à vontade para me perguntar e farei o meu melhor para ajudá-lo.";
	mes "^FFFFFFcobo^000000";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guia da cidade:Remova as marcas do mini-mapa:Notícias.:Cancelar")) {
		case 1:
			mes "[Guia de Geffen]";
			mes "Selecione um local no menu a seguir.";
			if (compass_check == 0) {
				mes "Você gostaria que eu marque os locais em seu mini-mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
//				next;
				switch(select("^FF0000Academia de Magia^000000:Loja de forjas:Loja de armas:Loja de ferramentas:PUB:Inn:Torre Geffen:Cancelar")) {
				case 1:
					mes "[Guia de Geffen]";
					mes "A Academia Mágica no noroeste de Geffen lida com mudanças de classes dos magos.";
					if (.@compass_check)
						viewpoint 1,61,180,2,0xFF0000;
					break;
				case 2:
					mes "[Guia de Geffen]";
					mes "A loja Forge está localizada a sudeste do centro de Geffen.";
					if (.@compass_check)
						viewpoint 1,182,59,3,0x00FF00;
					break;
				case 3:
					mes "[Guia de Geffen]";
					mes "A loja de armas pode ser encontrada a noroeste do centro de Geffen.";
					if (.@compass_check)
						viewpoint 1,99,140,4,0xFF00FF;
					break;
				case 4:
					mes "[Guia de Geffen]";
					mes "Você pode encontrar a loja de ferramentas indo para o sudoeste do centro de Geffen.";
					if (.@compass_check)
						viewpoint 1,44,86,5,0xFF00FF;
					break;
				case 5:
					mes "[Guia de Geffen]";
					mes "O PUB pode ser encontrado a nordeste a partir da Torre Geffen.";
					if (.@compass_check)
						viewpoint 1,138,138,6,0xFF00FF;
					break;
				case 6:
					mes "[Guia de Geffen]";
					mes "A pousada pode ser encontrada viajando para o nordeste do centro de Geffen.";
					if (.@compass_check)
						viewpoint 1,172,174,7,0xFF00FF;
					break;
				case 7:
					mes "[Guia de Geffen]";
					mes "Geffen Tower é encontrada no centro da cidade.";
					mes "A Guilda do Mago está no topo, e há até uma masmorra por baixo.";
					mes "Há muitos mistério em torno da torre...";
					if (.@compass_check)
						viewpoint 1,120,114,8,0x00FF00;
					break;
				case 8:
					mes "[Guia de Geffen]";
					mes "Por favor, peça-me para 'remover as marcas do mini-mapa' se você não deseja mais ter as marcas de localização exibidas no seu mini-mapa.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0x00FF00;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,134,221,6,0xFF00FF;
			viewpoint 2,204,214,7,0xFF00FF;
			viewpoint 2,204,214,8,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Geffen]";
			mes "Os avanços na feitiçaria e na tecnologia nos permitiram atualizar nosso sistema de informações, permitindo marcar locais em seu mini-mapa para facilitar a navegação.";
			next;
			mes "[Guia de Geffen]";
			mes "Seu mini-mapa está localizado no canto superior direito da tela.";
			mes "Se você não puder vê-lo, pressione as teclas CTRL + TAB ou clique no botão 'Mapa' na sua janela de informações básicas.";
			next;
			mes "[Guia de Geffen]";
			mes "No seu mini-mapa, clique nos símbolos '+' e '-' para aumentar o zoom e o seu mini-mapa.";
			mes "Esperamos que você aproveite suas viagens aqui na cidade de Geffen.";
			break;
		case 4:
			mes "[Guia de Geffen]";
			mes "Tudo bem, aventureiro.";
			mes "Desejo-lhe segurança em suas viagens pelas terras que você pode viajar...";
			mes "";
			mes "";
			close2;
			set .@loop1,0;
		}
	}
	cutin "gef_soldier",255;
	end;
}

geffen,118,62,0	duplicate(GefGuide)	Guide#2gef	705
