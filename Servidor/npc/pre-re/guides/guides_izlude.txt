// ===== Rathena script ========================================
// = guias izlude
// ===== por: =====================================================
// = kobra_k88;L0NE_W0LF
// ===== Versão atual: ======================================
// = 1.5a
// ===== Compatível com: =======================================
// = projeto Rathena
// ===== Descrição: ==========================================
// = [conversão oficial]
// = guias para a cidade de Izlude
// ===== Comentários adicionais: ===================================
// = 1.2 Nomes de guia comuns otimizados e atualizados [lúpus]
// = 1.3 Atualizou o guia de acordo com como é na IRO, obrigado [masterofmuppets]
// = para muad_dib
// = 1.4 ajustou as muitas linhas em 1 ou 2 e corrigiu alguns erros de digitação [erkurita]
// = 1,5 Rescrito aos padrões oficiais de 10.3.[L0ne_w0lf]
// = 1.5a corrigiu alguns erros com o loop [samuray22]
// -Obrigado a $ Ephiroth.
//============================================================

izlude,121,87,6	script	Guide#iz	105,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a Izlude, a cidade satélite de Protera.";
	mes "Se você precisar de alguma orientação em torno de Izlude, fique à vontade para me perguntar a qualquer momento.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Guia da cidade:Remova as marcas do mini-mapa:Notícias.:Cancelar")) {
		case 1:
			mes "[Guia de Izlude]";
			mes "Selecione um local no menu a seguir.";
			if (.@compass_check == 0) {
				mes "Você gostaria que eu marque os locais em seu mini-mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Swordman Association^000000:Swordman Hall:Arena:Izlude Marina:Loja de armas:Loja de ferramentas:Cancelar")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "A Swordman Association está localizado em uma ilha que está no oeste de Izlude.";
					mes "Se você está pensando em mudar a classe Swordman, você deve dar uma olhada.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "O Swordman Hall está localizado na ilha oriental conectada a Izlude.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "A famosa arena de Izlude está localizada no extremo norte de Izlude.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Você pode encontrar a marina na parte nordeste de Izlude.";
					mes "Lá, você pode montar um navio que o levará a Alberta ou Ilha Byalan.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Guia de Izlude]";
					mes "Você pode encontrar facilmente a loja de armas no noroeste de Izlude.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "A loja de ferramentas não deve ser muito difícil de encontrar na parte nordeste de Izlude.";
					mes "";
					mes "";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Guia de Izlude]";
					mes "Por favor, peça-me para 'remover as marcas do mini-mapa' se você não deseja mais ter as marcas de localização exibidas no seu mini-mapa.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Os avanços na feitiçaria e na tecnologia nos permitiram atualizar nosso sistema de informações, permitindo marcar locais em seu mini-mapa para facilitar a navegação.";
			next;
			mes "[Guia de Izlude]";
			mes "Seu mini-mapa está localizado no canto superior direito da tela.";
			mes "Se você não puder vê-lo, pressione as teclas CTRL + TAB ou clique no botão 'Mapa' na sua janela de informações básicas.";
			next;
			mes "[Guia de Izlude]";
			mes "No seu mini-mapa, clique nos símbolos '+' e '-' para aumentar o zoom e o seu mini-mapa.";
			mes "Esperamos que você aproveite suas viagens aqui na cidade de Izlude.";
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Ok, então, fique à vontade para vir até mim se você se sentir perdido em torno de Izlude, tudo bem?";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}
